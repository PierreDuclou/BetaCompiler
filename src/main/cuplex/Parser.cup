/**
 * Analyseur lexical
 *
 * Auteurs :
 *      Jérémy Thomas,
 *      Ouafa Bourekhsas,
 *      Salme Ould Ahmed,
 *      Théo Cominolo,
 *      Pierre Duclou
 *
 * Version SNAPSHOT-1.0
 */


/*
|------------------------------------------------------------------------------
| Header
|------------------------------------------------------------------------------
| This section includes the namespace declaration and all packages imports
|
*/

package generated.cuplex;

import ul.miage.l3.compilation.groupe_i.symbols.*;
import ul.miage.l3.compilation.groupe_i.ast.*;
import java.util.LinkedList;
import java.util.Arrays;


/*
|------------------------------------------------------------------------------
| Action code
|------------------------------------------------------------------------------
| Pure Java code (executed before the parsing)
|
*/

action code {:
    Integer context = null;
    SymbolsTable table = SymbolsTable.getInstance();
    AbstractSyntaxTree tree = AbstractSyntaxTree.getInstance();
    int lineNumber = 0;

    /**
     * Determine if a variable is global or local, then add it into the symbols table
     *
     * @param id id of the variable
     * @param type type of the variable
     * @param value value of the variable
     * @return key of the added variable in the symbols table
     */
    private int addVariable(String id, String type, int value) {
        int key;

        if (context == null) {
            key = table.add(new GlobalVariable(id, type, value));
        } else {
            Function f = (Function) table.get(context);
            int rank = (f.getNumberOfLocalVariables() + 2) * 4;
            key = table.add(new LocalVariable(id, type, rank, context, value));
            f.incrementNumberOfLocalVariables();
        }

        return key;
    }

    /**
     * Add a parameter into the symbols table
     *
     * @param id id of the variable
     * @param type type of the variable
     * @return key of the added parameter in the symbols table
     */
    private int addParameter(String id, String type) {
        Function f = (Function) table.get(context);
        int rank = (f.getNumberOfParameters() - 2) * 4;
        int key = table.add(new Parameter(id, type, rank, context));
        f.incrementNumberOfParameters();

        return key;
    }

    /**
     * Returns a node that represents an if statement without else clause
     *
     * @param condition node that represents the logical expression of the condition
     * @param _then list of node representing instructions executed if the condition is verified
     * @return node that represents an if statement
     */
    private InnerNode createIfNode(Node condition, LinkedList<Node> _then) {
        InnerNode ret = new InnerNode(NodeSymbol.IF, -999);
        ret.addChild(condition);
        ret.addChild(new InnerNode(NodeSymbol.THEN, -999, _then));
        return ret;
    }

    /**
     * Returns a node that represents an if statement with an else clause
     *
     * @param condition node that represents the logical expression of the condition
     * @param _then list of node representing instructions executed if the condition is verified
     * @param _else list of node representing instructions executed if the condition is not verified
     * @return node that represents an if statement
     */
    private InnerNode createIfNode(Node condition, LinkedList<Node> _then, LinkedList<Node> _else) {
        InnerNode ret = createIfNode(condition, _then);
        ret.addChild(new InnerNode(NodeSymbol.ELSE, -999, _else));
        return ret;
    }

    /**
     * Returns a node representing an expression (can be logical or arithmetical)
     *
     * @param symbol operator
     * @param left left member
     * @param right right member
     * @return node representing a comparison
     */
    private InnerNode createExpressionNode(NodeSymbol symbol, Node left, Node right) {
        return new InnerNode(symbol, -999, new LinkedList(Arrays.asList(new Node[] { left, right })));        
    }
:};


/*
|------------------------------------------------------------------------------
| Gramar declaration
|------------------------------------------------------------------------------
| This section includes all terminals and non terminals declaration of the gramar
|
*/

// Verbose tokens
terminal INT, VOID, RETURN, IF, ELSE, WHILE;

// Delimiters
terminal SEMICOLON, COMMA, ARROW;

// Operators
terminal ADD, SUB, DIV, MUL, ASSIGN, LT, GT, GE, LE, EQ, DIFF;

// Wrappers
terminal OPEN_P, CLOSE_P, OPEN_B, CLOSE_B;

// Built-in function
terminal READ, WRITE;

// Custom tokens
terminal ID, VALUE;

// Non terminals
non terminal entry;
non terminal LinkedList<Node> program;
non terminal InnerNode func_definition;
non terminal func_type;
non terminal parameters;
non terminal InnerNode func_call;
non terminal LinkedList<Node> arguments;
non terminal LinkedList<Node> block;
non terminal LinkedList<Node> inner_block;
non terminal InnerNode control_flow;
non terminal InnerNode if_statement;
non terminal InnerNode condition;
non terminal InnerNode comparison;
non terminal InnerNode while_loop;
non terminal InnerNode instruction;
non terminal InnerNode concrete_instruction;
non terminal Node expression;
non terminal Node term;
non terminal Node factor;
non terminal InnerNode return_statement;
non terminal InnerNode var_declaration;
non terminal InnerNode var_assignation;


/*
|------------------------------------------------------------------------------
| Productions
|------------------------------------------------------------------------------
| This section includes all productions of the gramar
|
*/

start with entry;

entry ::= program:p
        {:
            InnerNode root = new InnerNode(NodeSymbol.PROGRAM, -999);
            root.addChild(p);
            tree.setRoot(root);
        :};

program ::= program var_declaration SEMICOLON {: RESULT = new LinkedList(); :}
          | program var_assignation SEMICOLON {: RESULT = new LinkedList(); :}
          | program:p func_definition:f {: p.add(f); RESULT = p; :}
          | {: RESULT = new LinkedList(); :};
          
//--- Function ----------------------------------------------------------------
func_definition ::= ID:id {: context = table.add(new Function((String) id)); :}
                    OPEN_P parameters
                    CLOSE_P ARROW func_type:t block:b
                    {:
                        Function f = (Function) table.get(context);
                        f.setType((String) t);
                        RESULT = new InnerNode(NodeSymbol.FUNCTION, context, b);
                        context = null;
                    :};

func_type       ::= INT:i {: RESULT = i; :} | VOID:v {: RESULT = v; :};
parameters      ::= parameters COMMA var_declaration
                  | var_declaration
                  | ;
func_call       ::= ID:id OPEN_P arguments:a CLOSE_P {: RESULT = new InnerNode(NodeSymbol.CALL, -999, a); :}
                  | READ OPEN_P arguments:a CLOSE_P {: RESULT = new InnerNode(NodeSymbol.READ, -999, a); :}
                  | WRITE OPEN_P arguments:a CLOSE_P {: RESULT = new InnerNode(NodeSymbol.WRITE, -999, a); :};

arguments       ::= arguments:a COMMA expression:ex {: a.add(ex); :}
                  | expression:e {: RESULT = new LinkedList(Arrays.asList(new Node[] {e})); :}
                  | {: RESULT = new LinkedList(); :};

//--- Block -------------------------------------------------------------------
block       ::= OPEN_B block inner_block CLOSE_B
              | OPEN_B inner_block:i CLOSE_B {: RESULT = i; :};
inner_block ::= inner_block:in instruction:i
                {:
                    if (in != null) {
                        in.add(i);
                        RESULT = in;
                    } else {
                        RESULT = new LinkedList();
                    }
                :}
              | inner_block:i control_flow:c {: i.add(c); RESULT = i; :}
              | {: RESULT = new LinkedList(); :};

//--- Control flow ------------------------------------------------------------
control_flow ::= if_statement:i {: RESULT = i; :} | while_loop:w {: RESULT = w; :};

// IF statement
if_statement ::= IF condition:c block:b {: RESULT = createIfNode(c, b); :}
               | IF condition:c block:b1 ELSE block:b2 {: RESULT = createIfNode(c, b1, b2); :};

condition  ::= OPEN_P comparison:c CLOSE_P {: RESULT = c; :};
comparison ::= expression:e1 LT expression:e2 {: RESULT = createExpressionNode(NodeSymbol.LT, e1, e2); :}
            | expression:e1 LE expression:e2 {: RESULT = createExpressionNode(NodeSymbol.LE, e1, e2); :}
            | expression:e1 EQ expression:e2 {: RESULT = createExpressionNode(NodeSymbol.EQ, e1, e2); :}
            | expression:e1 DIFF expression:e2 {: RESULT = createExpressionNode(NodeSymbol.DIFF, e1, e2); :}
            | expression:e1 GE expression:e2 {: RESULT = createExpressionNode(NodeSymbol.GE, e1, e2); :}
            | expression:e1 GT expression:e2 {: RESULT = createExpressionNode(NodeSymbol.GT, e1, e2); :};

// While loop
while_loop ::= WHILE condition:c block:b {: b.addFirst(c); RESULT = new InnerNode(NodeSymbol.WHILE, -999, b); :};

//--- Instruction -------------------------------------------------------------
instruction          ::= concrete_instruction:c SEMICOLON {: RESULT = c; :};
concrete_instruction ::= var_declaration:vd {: RESULT = vd; :}
                       | var_assignation:va {: RESULT = va; :}
                       | return_statement:r {: RESULT = r; :}
                       | func_call:f {: RESULT = f; :};

// Variable declaration
var_declaration ::= INT:t ID:id ASSIGN expression:e
                    {:
                        // Resolving key
                        int key;

                        if (e instanceof LeafNode) {
                            LeafNode tmp = (LeafNode) e;
                            key = addVariable(id.toString(), t.toString(), tmp.getValue());
                        } else {
                            key = addVariable(id.toString(), t.toString(), 0);
                        }

                        // If the variable is local
                        if (context != null) {
                            RESULT = new InnerNode(NodeSymbol.ASSIGN);
                            LeafNode left = new LeafNode(NodeSymbol.VARIABLE, key);
                            RESULT.addChild(left);
                            RESULT.addChild(e);
                        }
                    :}
                  | INT:t ID:id
                    {:
                        if (context != null)
                            addParameter(id.toString(), t.toString());
                        else
                            addVariable(id.toString(), t.toString(), 0);
                    :};

// Variable assignation
var_assignation  ::= ID:id ASSIGN expression:e
                    {:
                        RESULT = new InnerNode(NodeSymbol.ASSIGN);
                        RESULT.addChild(new LeafNode(NodeSymbol.VARIABLE, table.getClosestVariableKey(id.toString(), context)));
                        RESULT.addChild(e);
                    :};

// Return statement
return_statement ::= RETURN expression:e 
                    {:
                        InnerNode tmp = new InnerNode(NodeSymbol.RETURN, -999);
                        tmp.addChild(e);
                        RESULT = tmp;
                    :};

// Expression
expression ::= term:t ADD expression:e {: RESULT = createExpressionNode(NodeSymbol.ADD, t, e); :}
             | term:t SUB expression:e {: RESULT = createExpressionNode(NodeSymbol.SUB, t, e); :}
             | term:t {: RESULT = t; :};
term       ::= factor:f MUL term:t {: RESULT = createExpressionNode(NodeSymbol.MUL, f, t); :}
             | factor:f DIV term:t {: RESULT = createExpressionNode(NodeSymbol.DIV, f, t); :}
             | factor:f {: RESULT = f; :};
factor     ::= OPEN_P expression:e CLOSE_P {: RESULT = e; :}
             | func_call:f {: RESULT = f; :}
             | VALUE:v {: RESULT = new LeafNode(NodeSymbol.CONST, -999, v.toString()); :}
             | ID:id {: RESULT = new LeafNode(NodeSymbol.VARIABLE, table.getClosestVariableKey(id.toString(), context)); :};
